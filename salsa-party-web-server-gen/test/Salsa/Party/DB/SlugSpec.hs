{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE TypeApplications #-}

module Salsa.Party.DB.SlugSpec (spec) where

import Salsa.Party.DB
import Salsa.Party.Web.Server.Gen ()
import Test.Syd
import Test.Syd.Validity

spec :: Spec
spec = do
  genValidSpec @EventSlug
  modifyMaxSize (* 10) $
    modifyMaxSuccess (* 10) $
      describe "mkSlug" $ do
        it "produces valid slugs" $
          producesValidsOnValids mkSlug

        it "is idempotent" $
          forAllValid $ \text ->
            case mkSlug text of
              Nothing -> pure ()
              Just slug1 -> case mkSlug (unSlug slug1) of
                Nothing -> expectationFailure "Should have been able to slug an existing slug"
                Just slug2 -> slug2 `shouldBe` slug1

        it "deals with these fancy diacritics" $
          case mkSlug "Ã¡Ä…Ã¤ÃÄ„Ã„ Ã©Ä™Ã«Ã‰Ä˜Ã‹ Ã­Ä¯Ã¯ÃÄ®Ã Ç«Ã³Ã¶ÇªÃ“Ã– ÃºÅ³Ã¼ÃšÅ²Ãœ" of
            Nothing -> expectationFailure "Should have made a valid slug"
            Just s -> do
              shouldBeValid s
              s `shouldBe` Slug "aaaaaa-eeeeee-iiiiii-oooooo-uuuuuu"

        it "deals with these german letters" $
          case mkSlug "ÃŸ" of
            Nothing -> expectationFailure "Should have made a valid slug"
            Just s -> do
              shouldBeValid s
              s `shouldBe` Slug "ss"

        it "produces a nice slug for this title" $
          case mkSlug "Bachata Community ZÃ¼rich Mondays ðŸ’ƒðŸ•º" of
            Nothing -> expectationFailure "Should have made a valid slug"
            Just s -> do
              shouldBeValid s
              s `shouldBe` Slug "bachata-community-zurich-mondays"

        it "produces a nice slug for this title" $
          case mkSlug "ðŸŒŸ NOCHE LATINA ðŸŒŸ  Salsa On2 Workshop und  Party mit DJ Raffi " of
            Nothing -> expectationFailure "Should have made a valid slug"
            Just s -> do
              shouldBeValid s
              s `shouldBe` Slug "noche-latina-salsa-on2-workshop-und-party-mit-dj-raffi"
